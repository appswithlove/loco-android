import com.vanniktech.maven.publish.GradlePlugin
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.vanniktech.maven.publish'
    id 'com.gradle.plugin-publish'
}

group GROUP
version VERSION_NAME

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntimeOnly 'cglib:cglib-nodep:3.2.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

mavenPublishing {
    configure(new GradlePlugin(new JavadocJar.Javadoc(), true))
}

gradlePlugin {
    website = project.findProperty("POM_URL") ?: ""
    vcsUrl = project.findProperty("POM_SCM_URL") ?: ""

    plugins {
        loco {
            id = project.findProperty("GROUP") ?: ""
            displayName = project.findProperty("POM_NAME") ?: ""
            description = project.findProperty("POM_DESCRIPTION") ?: ""
            version = project.findProperty("VERSION_NAME") ?: ""
            tags = ['loco', 'localization', 'android']
            implementationClass = "com.appswithlove.loco.LocoPlugin"
        }
    }
}


afterEvaluate {
    tasks.findByName('generateMetadataFileForPluginMavenPublication')?.dependsOn('plainJavadocJar')
    tasks.findByName('generatePomFileForPluginMavenPublication')?.dependsOn('plainJavadocJar')
}


